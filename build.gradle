buildscript {
    dependencies {
        classpath 'com.github.fgiannesini.libsass.gradle.plugin:libsass-gradle-plugin:+'
    }
}

plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.7'
    id 'com.github.ben-manes.versions' version '0.21.0'
    id 'org.beryx.jlink' version '2.11.1'
    id 'org.javamodularity.moduleplugin' version '1.5.0'
    id 'jacoco'
}

apply plugin: 'com.github.fgiannesini.libsass.gradle.plugin'

version '0.1.0'

sourceCompatibility = 1.11

repositories {
    jcenter()
}

dependencies {
    // Junit (Unit testing)
    testImplementation 'org.junit.jupiter:junit-jupiter:5.4.2'

    // TestFX (UI testing)
    testCompile 'org.testfx:testfx-core:4.0.15-alpha'
    testCompile 'org.testfx:testfx-junit5:4.0.15-alpha'

    // Commons Collections https://mvnrepository.com/artifact/org.apache.commons/commons-collections4
    compile 'org.apache.commons:commons-collections4:4.3'

    // Commons Lang https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    compile 'org.apache.commons:commons-lang3:3.9'

    // ControlsFX https://mvnrepository.com/artifact/org.controlsfx/controlsfx
    compile 'org.controlsfx:controlsfx:11.0.0'

    // Caffeine https://mvnrepository.com/artifact/com.github.ben-manes.caffeine/caffeine
    compile 'com.github.ben-manes.caffeine:caffeine:2.7.0'
}

javafx {
    version = '12.0.1'
    modules = ['javafx.controls']
}

test {
    useJUnitPlatform()

    jvmArgs '-Dtestfx.robot=awt'

    moduleOptions {
        runOnClasspath = true
    }

    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
        csv.enabled = false
    }
}

libSassParameters {
    inputFilePath = "$projectDir/src/main/resources/stylesheets/scss/styles.scss"
    outputFilePath = "$projectDir/src/main/resources/stylesheets/css/styles.css"
    watchedDirectoryPath = "scss"
    sourceComments = true
}

compileJava.finalizedBy('compileLibSass')

mainClassName = "$moduleName/BoundingBoxEditor.BoundingBoxEditorApp"

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'boundingboxeditor'
    }

    imageZip = project.file("${buildDir}/distributions/boundingboxeditor-${javafx.platform.classifier}.zip")
}

