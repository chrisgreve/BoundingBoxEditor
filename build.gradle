buildscript {
    dependencies {
        classpath 'com.github.fgiannesini.libsass.gradle.plugin:libsass-gradle-plugin:+'
    }
}

plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id 'com.github.ben-manes.versions' version '0.33.0'
    id 'org.beryx.jlink' version '2.22.0'
    id 'jacoco'
    id 'net.ltgt.errorprone' version '1.2.1'
    id 'org.sonarqube' version '3.0'
}

apply plugin: 'com.github.fgiannesini.libsass.gradle.plugin'

version '2.1.1'

sourceCompatibility = 1.11

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    // Junit (Unit testing)
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.0'

    // TestFx UI TesTing // https://mvnrepository.com/artifact/org.testfx/testfx-junit5
    testImplementation('org.testfx:testfx-junit5:4.0.16-alpha') {
        exclude group: 'org.hamcrest'
    }

    // Hamcrest https://mvnrepository.com/artifact/org.hamcrest/hamcrest
    testImplementation 'org.hamcrest:hamcrest:2.2'

    // Commons Collections https://mvnrepository.com/artifact/org.apache.commons/commons-collections4
    implementation 'org.apache.commons:commons-collections4:4.4'

    // Commons Lang https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    implementation 'org.apache.commons:commons-lang3:3.11'

    // ControlsFX https://mvnrepository.com/artifact/org.controlsfx/controlsfx
    implementation('org.controlsfx:controlsfx:11.0.2') {
        exclude group: 'org.openjfx'
    }

    // Caffeine https://mvnrepository.com/artifact/com.github.ben-manes.caffeine/caffeine
    implementation 'com.github.ben-manes.caffeine:caffeine:2.8.5'

    // Gradle plugin to use the error-prone compiler https://github.com/tbroyer/gradle-errorprone-plugin
    errorprone 'com.google.errorprone:error_prone_core:2.4.0'

    // Google GSON https://github.com/google/gson
    implementation 'com.google.code.gson:gson:2.8.6'
}

javafx {
    version = '14'
    modules = ['javafx.controls']
}

test {
    useJUnitPlatform()

    jvmArgs = ['-Dtestfx.robot=awt', '-Xmx3072m', '-Dprism.forceGPU=true']

    moduleOptions {
        runOnClasspath = true
    }

    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
        csv.enabled = false
    }
}

libSassParameters {
    inputFilePath = "${projectDir}/src/main/resources/stylesheets/scss/styles.scss"
    outputFilePath = "${projectDir}/src/main/resources/stylesheets/css/styles.css"
    watchedDirectoryPath = "scss"
    sourceComments = true
}

compileJava {
    options.compilerArgs.addAll(['-Xlint:all', '-Xlint:-requires-automatic'])
}

compileJava.finalizedBy('compileLibSass')

application {
    mainModule = "boundingboxeditor"
    mainClass = "boundingboxeditor.BoundingBoxEditorApp"
    applicationDefaultJvmArgs = ['-Dprism.forceGPU=true']
}

java {
    modularity.inferModulePath = true
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'BoundingBoxEditor'
        jvmArgs = ['-Dprism.forceGPU=true']
    }

    imageZip = project.file("${buildDir}/distributions/boundingboxeditor-${javafx.platform.classifier}.zip")

    jpackage {
        installerOptions += [
                '--app-version', version,
                '--copyright', 'Copyright 2020, Markus Fleischhacker',
                '--license-file', 'LICENSE',
                '--vendor', 'Markus Fleischhacker'
        ]

        if(org.gradle.internal.os.OperatingSystem.current().windows) {
            installerOptions += [
                    '--win-per-user-install',
                    '--win-dir-chooser',
                    '--win-menu',
                    '--win-menu-group', 'BoundingBoxEditor',
                    '--win-shortcut'
            ]
            icon = 'src/main/resources/icons/app_icon.ico'
            installerType = 'exe'
        } else if(org.gradle.internal.os.OperatingSystem.current().linux) {
            installerOptions += [
                    '--resource-dir', 'build/jpackage/BoundingBoxEditor/lib',
                    '--linux-shortcut',
                    '--linux-menu-group', 'Science'
            ]
            icon = 'src/main/resources/icons/app_icon.png'
        } else if(org.gradle.internal.os.OperatingSystem.current().macOsX) {
            icon = 'src/main/resources/icons/app_icon.icns'
        }
    }
}

task dist {
    dependsOn clean, jlinkZip
}

sonarqube {
    properties {
        property "sonar.projectKey", "mfl28_BoundingBoxEditor"
    }
}

